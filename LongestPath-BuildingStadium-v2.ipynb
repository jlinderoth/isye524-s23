{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# A Longest Path Example -- Building a Stadium\n",
    "\n",
    "We have the following information about the tasks that need to be completed to build a stadium:\n",
    "\n",
    "\n",
    "|Task | Duration (weeks) | Predecessors |\n",
    "|----:|---------:|-------------:|\n",
    "|1|2|none|\n",
    "|2|16|1|\n",
    "|3|9|2|\n",
    "|4|8|2|\n",
    "|5|10|3|\n",
    "|6|6|4,5|\n",
    "|7|2|4|\n",
    "|8|2|6|\n",
    "|9|9|4,6|\n",
    "|10|5|4|\n",
    "|11|3|6|\n",
    "|12|2|9|\n",
    "|13|1|7|\n",
    "|14|7|2|\n",
    "|15|4|4,14|\n",
    "|16|3|8,11,14|\n",
    "|17|9|12|\n",
    "|18|1|17|\n",
    "\n",
    "What is the shortest time in which we could complete the entire stadium? Which tasks are on the so-called \"critical path,\" or the sequence of activities that will take the longest to complete?\n",
    "\n",
    "Here we will model this finding the maximum length path from start to finish in the project network.\n",
    "It is again a MCNF model, where nodes are the set of tasks $T$.  There is an arc between tasks $i \\in T$ and $j \\in T$ if $i$ directly precedes $j$.\n",
    "$b_i = 1$ for the starting task, and $b_i = -1$ for the finishing task.  (In modeling these in practice, you may need to create a dummy starting task and a dummy finishing tasks, connecting tasks that have no predecessors to the starting task, and connecting tasks that have no antecedents to the finishing task).\n",
    "The cost of arc $(i,j)$ is the time to complete task $j$.\n",
    "\n",
    "## Building the Data and Model in Julia "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Longest path (shortest completion time) is 64.0\n",
      "S,1 is on longest path\n",
      "1,2 is on longest path\n",
      "2,3 is on longest path\n",
      "3,5 is on longest path\n",
      "5,6 is on longest path\n",
      "6,9 is on longest path\n",
      "9,12 is on longest path\n",
      "12,17 is on longest path\n",
      "17,18 is on longest path\n",
      "18,F is on longest path\n",
      "0; Iter: Time   1.907e-08; average =   1.907e-09; Bound =   1.915e-06\n"
     ]
    }
   ],
   "source": [
    "using JuMP, HiGHS\n",
    "\n",
    "### DATA ###\n",
    "\n",
    "# task list\n",
    "T = [:S, :1, :2, :3, :4, :5, :6, :7, :8, :9, :10, :11, :12, :13, :14, :15, :16, :17, :18, :F]\n",
    "\n",
    "# each task has a duration\n",
    "duration = Dict(zip(T,[0,2,16,9,8,10,6,2,2,9,5,3,2,1,7,4,3,9,1,0]))\n",
    "\n",
    "pred = [(:S,:1), (:1, :2), (:2, :3), (:2,:4), (:2,:14), (:3,:5), (:4,:6), (:4,:7), (:4,:9), (:4,:15), (:5,:6), (:6,:8), (:6,:9), (:6,:11), (:7,:13), (:8,:16), (:9,:12), (:10,:F), (:11,:16), (:12,:17), (:13,:F), (:14,:15), (:14,:16), (:15,:F), (:16,:F), (:17,:18), (:18,:F)] \n",
    "\n",
    "# Now we can set the costs \n",
    "c = Dict()\n",
    "for i in T\n",
    "    for j in T\n",
    "        if (i,j) in pred\n",
    "            c[i,j] = duration[j]\n",
    "        end\n",
    "    end\n",
    "end \n",
    "\n",
    "# Net supply \n",
    "b = Dict(i => 0 for i in T)\n",
    "b[:S] = 1\n",
    "b[:F] = -1\n",
    "\n",
    "# # ### MODEL ###\n",
    "\n",
    "m = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(m, 0 <= x[pred] <= 1) # arcs in our longest path network\n",
    "\n",
    "@objective(m, Max, sum(c[a]*x[a] for a in pred))\n",
    "\n",
    "@constraint(m, flow_balance[k in T], sum(x[(k,j)] for j in T if (k,j) in pred) - sum(x[(i,k)] for i in T if (i,k) in pred) == b[k])\n",
    "\n",
    "\n",
    "set_silent(m)\n",
    "# # # solve this instance of the longest path problem\n",
    "optimize!(m)\n",
    "\n",
    "\n",
    "println(\"Longest path (shortest completion time) is \", round(objective_value(m),digits=1))\n",
    "for (i,j) in pred\n",
    "    if value(x[(i,j)]) > 0.1\n",
    "        println(i, \",\", j, \" is on longest path\")\n",
    "    end \n",
    "end \n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.3",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
